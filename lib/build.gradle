plugins {
    id 'java-library'
    id 'jacoco'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'com.nikialeksey:goo:0.0.2'
    implementation 'org.cactoos:cactoos:0.50'
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.22.1'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jacoco {
    toolVersion = "0.8.5"
}
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

check.dependsOn jacocoTestReport

if (project.rootProject.file('local.properties').exists()) {
    ext {
        artifact = 'java-inspections'
        publishedGroupId = 'dev.youshallnotpass'
        libraryName = 'java-inspections'

        libraryDescription = 'Java inspections library for youshallnotpass'

        siteUrl = 'https://github.com/youshallnotpass-dev/java-inspections'
        gitUrl = 'https://github.com/youshallnotpass-dev/java-inspections.git'

        libraryVersion = '0.9.4'

        developerId = 'nikialeksey'
        developerName = 'Alexey Nikitin'
        developerEmail = 'nikialeksey@gmail.com'
        licenseName = 'MIT License'
        licenseUrl = 'https://github.com/youshallnotpass-dev/java-inspections/blob/master/LICENSE'
    }
    version = libraryVersion

    Properties properties = new Properties()
    properties.load(
            project.rootProject.file('local.properties').newDataInputStream()
    )

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        repositories {
            maven {
                name = 'central'
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username properties.getProperty("sonatype.user")
                    password properties.getProperty("sonatype.pass")
                }
            }
        }

        publications {
            lib(MavenPublication) {
                artifact(javadocJar)
                artifact(sourcesJar)
                artifact("$buildDir/libs/lib-${libraryVersion}.jar")

                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                pom {
                    name = libraryName
                    description = libraryDescription

                    url = siteUrl

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', 'com.nikialeksey')
                    dependencyNode.appendNode('artifactId', 'goo')
                    dependencyNode.appendNode('version', '0.0.2')

                    dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', 'org.cactoos')
                    dependencyNode.appendNode('artifactId', 'cactoos')
                    dependencyNode.appendNode('version', '0.50')

                    dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', 'com.github.javaparser')
                    dependencyNode.appendNode('artifactId', 'javaparser-symbol-solver-core')
                    dependencyNode.appendNode('version', '3.22.1')

                    dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', 'org.apache.httpcomponents.client5')
                    dependencyNode.appendNode('artifactId', 'httpclient5')
                    dependencyNode.appendNode('version', '5.1')
                }
            }
        }
    }

    def signingKeyId = properties.getProperty("signingKeyId")
    def signingKey = new File(properties.getProperty("signingKey")).text
    def signingPassword = properties.getProperty("signingPassword")
    signing {
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign(publishing.publications.lib)
    }

    nexusStaging {
        serverUrl = "https://s01.oss.sonatype.org/service/local/"
        username = properties.getProperty("sonatype.user")
        password = properties.getProperty("sonatype.pass")
        packageGroup = publishedGroupId
    }
}